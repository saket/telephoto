plugins {
  id("com.android.application")
  id("kotlin-android")
  id("kotlin-parcelize")
}

android {
  namespace "me.saket.telephoto.sample"

  defaultConfig {
    applicationId namespace
    minSdkVersion 31
    compileSdk 33
    versionCode 1
    versionName "1.0"
  }
  signingConfigs {
    release {
      keyAlias = "sample"
      keyPassword = "frappe-snivel-possible-downward"
      storeFile = file("release_keystore.jks")
      storePassword = "abstract-emperor-john-twill"
    }
  }
  buildTypes {
    release {
      minifyEnabled true
      signingConfig signingConfigs.release
    }
  }
  lint {
    lintConfig = file("lint.xml")
  }
  buildFeatures.compose = true
  composeOptions.kotlinCompilerExtensionVersion = libs.versions.androidx.compose.compiler.get()
}

dependencies {
  implementation(projects.zoomable)
  implementation(projects.zoomableImage.core)
  implementation(projects.zoomableImage.coil)
  implementation(projects.zoomableImage.glide)

  implementation(libs.androidx.appcompat)
  implementation(libs.androidx.ktx.core)
  implementation(libs.androidx.ktx.palette)
  implementation(libs.androidx.activity)
  implementation(libs.compose.ui.material3)
  implementation(libs.compose.ui.materialIcons)

  implementation(libs.coil.compose)
  implementation(libs.coil.gif)
  implementation(libs.appyx)

  if (System.getenv("CI") == null) {
    // Use a newer version of compose that fixes state restoration with pager.
    // Using different versions in different modules interferes with lint on CI
    // for some reason so this is only run locally.
    implementation("androidx.compose.foundation:foundation:1.5.0-alpha03")
  }
}
